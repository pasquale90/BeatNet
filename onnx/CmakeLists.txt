cmake_minimum_required(VERSION 3.22)
set(LIBRARY_NAME beatnet)
set(APP_NAME beatnet_infer)
project(${LIBRARY_NAME})

set(CMAKE_CXX_STANDARD 17)

option(ENABLE_KISSFFT "Use the Kiss FFT library instead of FFTW3" ON)
option(ENABLE_FFTW3 "Use the FFTW3 library instead of Kiss FFT" OFF)
if(ENABLE_KISSFFT AND ENABLE_FFTW3)
    message(FATAL_ERROR "ENABLE_KISSFFT and ENABLE_FFTW3 cannot both be ON. Choose one.")
elseif(NOT ENABLE_KISSFFT AND NOT ENABLE_FFTW3)
    message(FATAL_ERROR "No FFT backend selected.Please enable either ENABLE_KISSFFT or ENABLE_FFTW3.")
endif()

if(ENABLE_KISSFFT)
    message(STATUS "FFT backend: KissFFT")
elseif(ENABLE_FFTW3)
    message(STATUS "FFT backend: FFTW3")
else()
    message(WARNING "No FFT backend selected!")
endif()

# Set the libraries directories. Change if necessary. 
# TODO: There is space for improvement here..
set(ORT_DIR ${CMAKE_CURRENT_SOURCE_DIR}/onnxruntime)
set(LIBSAMPLERATE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/libsamplerate)
if(ENABLE_FFTW3)
    set(FFTW3_DIR ${CMAKE_CURRENT_SOURCE_DIR}/fftw)
elseif(ENABLE_KISSFFT)
    set(KISSFFT_DIR ${CMAKE_CURRENT_SOURCE_DIR}/kissfft)
endif()

option(BUILD_APP "Build the test application using main.cpp" OFF)

set(BEATNET_INCLUDE_DIRS
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${ORT_DIR}/include
    ${LIBSAMPLERATE_DIR}/include
)

set(BEATNET_SUBLIBS
    ${ORT_DIR}/lib/onnxruntime.lib
    ${LIBSAMPLERATE_DIR}/lib/samplerate.lib
)

if(ENABLE_FFTW3)
    list(APPEND BEATNET_INCLUDE_DIRS ${FFTW3_DIR})
    list(APPEND BEATNET_SUBLIBS ${FFTW3_DIR}/fftw3f.lib)
elseif(ENABLE_KISSFFT)
    list(APPEND BEATNET_INCLUDE_DIRS ${KISSFFT_DIR})
    add_library(libkissfft STATIC
        ${KISSFFT_DIR}/kiss_fft.c
        ${KISSFFT_DIR}/kiss_fftr.c
    )
    list(APPEND BEATNET_SUBLIBS libkissfft)
endif()

set(LIB_SOURCE_FILES  
    BeatNet.cpp 
    resampler.cpp
    frameprocessor.cpp
    fftprocessor.cpp
    filterbankprocessor.cpp
    logspecutils.cpp
)

set(SOURCE_FILE
    main.cpp
)

add_library(${LIBRARY_NAME} STATIC ${LIB_SOURCE_FILES})

target_include_directories(${LIBRARY_NAME} PUBLIC ${BEATNET_INCLUDE_DIRS})
target_link_libraries(${LIBRARY_NAME} PUBLIC ${BEATNET_SUBLIBS})


if(ENABLE_FFTW3)
    target_compile_definitions(${LIBRARY_NAME} PUBLIC ENABLE_FFTW3)
elseif(ENABLE_KISSFFT)
    target_compile_definitions(${LIBRARY_NAME} PUBLIC ENABLE_KISSFFT)
endif()

if(BUILD_APP)
    add_executable(${APP_NAME} ${SOURCE_FILE})
    target_link_libraries(${APP_NAME} PRIVATE ${LIBRARY_NAME})
endif()

if (BUILD_APP AND WIN32)

    set(DLLS
        "${ORT_DIR}/lib/onnxruntime.dll"
        "${LIBSAMPLERATE_DIR}/bin/samplerate.dll"
    )
    if(ENABLE_FFTW3)
        list(APPEND DLLS "${FFTW3_DIR}/libfftw3f-3.dll")
    endif()

    add_custom_command(TARGET ${APP_NAME} POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            ${DLLS}
            $<TARGET_FILE_DIR:${APP_NAME}>
    )
endif()